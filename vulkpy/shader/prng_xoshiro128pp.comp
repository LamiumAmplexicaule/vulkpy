#version 460
layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;


layout(push_constant) uniform constants {
  uint shift;
  uint size;
} params;


layout(std430, binding = 0) buffer A {
  uint a[];
};
layout(std430, binding = 1) writeonly buffer B {
  float b[];
};


uint rotl(uint x, int k){
  return (x << k) | (x >> (32 - k));
}


// xoshiro128++
// https://prng.di.unimi.it/xoshiro128plusplus.c
void main(){
  uint i = gl_GlobalInvocationID.x;
  if(i+params.shift >= params.size){ return; }
  uint j = 4 * i;

  uint result = rotl(a[j] + a[j+3], 7) + a[j];
  b[i+params.shift] = uintBitsToFloat((result >> 9) | 0x3f800000) - 1.0;

  uint t = (a[j+1] << 9);

  a[j+2] ^= a[j  ];
  a[j+3] ^= a[j+1];
  a[j+1] ^= a[j+2];
  a[j  ] ^= a[j+3];

  a[j+2] ^= t;
  a[j+3] = rotl(a[j+3], 11);
}
